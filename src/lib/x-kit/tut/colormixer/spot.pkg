## spot.pkg
#
# A trivial widget which displays a settable color.

# Compiled by:
#     src/lib/x-kit/tut/colormixer/colormixer-app.lib

stipulate
    #
    package lbl =  label;				# label				is from   src/lib/x-kit/widget/old/leaf/label.pkg
    package rw  =  root_window_old;			# root_window_old		is from   src/lib/x-kit/widget/old/basic/root-window-old.pkg
    package wg  =  widget;				# widget			is from   src/lib/x-kit/widget/old/basic/widget.pkg
    package wa  =  widget_attribute_old;		# widget_attribute_old		is from   src/lib/x-kit/widget/old/lib/widget-attribute-old.pkg
    package sz  =  size_preference_wrapper;		# size_preference_wrapper	is from   src/lib/x-kit/widget/old/wrapper/size-preference-wrapper.pkg
    #
    package xc  =  xclient;				# xclient			is from   src/lib/x-kit/xclient/xclient.pkg
    package g2d =  geometry2d;				# geometry2d			is from   src/lib/std/2d/geometry2d.pkg
herein

    package   spot
    :         Spot					# Spot				is from   src/lib/x-kit/tut/colormixer/spot.api
    {
	Spot = SPOT (wg::Widget, (xc::Rgb -> Void));

	fun make_spot
	      (root_window, view)
	      { color => rgb,
		wide,
		high
	      }
	    =
	    {   color_of = rw::color_of  root_window;

		(xc::rgb_to_unts rgb) ->  (red, green, blue);

		color = xc::CMS_RGB { red, green, blue };

		args  = [ (wa::background, wa::COLOR_VAL (color_of color)) ];

		label = lbl::make_label' (root_window, view, args);

		widget
		    =
		    sz::make_tight_sized_preference_wrapper
		      ( lbl::as_widget  label,
			{ wide, high }
		      );

		fun set_color  rgb
		    = 
		    {   (xc::rgb_to_unts rgb)
			    ->
			    (red, green, blue);

			lbl::set_background
			    label
			    (color_of (xc::CMS_RGB { red, green, blue }));
		    };

		SPOT (widget, set_color);
	    };

	fun as_widget (SPOT (widget, _        )) =  widget;
	fun set_spot  (SPOT (_,      set_color)) =  set_color;

    };							# package spot 

end;

## COPYRIGHT (c) 1991, 1995 by AT&T Bell Laboratories.  See SMLNJ-COPYRIGHT file for details.
## Subsequent changes by Jeff Prothero Copyright (c) 2010-2015,
## released per terms of SMLNJ-COPYRIGHT.
