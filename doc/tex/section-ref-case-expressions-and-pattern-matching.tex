
% --------------------------------------------------------------------------------
\subsection{Case Expression}
\cutdef*{subsubsection}
\label{section:ref:case-expressions-and-pattern-matching:case-expression}

At its simplest, the Mythryl {\tt case} expression may 
be used much like the C {\tt switch} statement:

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    i = 3;

    case i
        1 => print "One.\n";
        2 => print "Two.\n";
        3 => print "Three.\n";
        _ => print "Dunno.\n";
    esac;

    linux$ ./my-script
    Three.
\end{verbatim}

Here the underbar pattern serves as an ``other'' case, 
catching any value not handled by any of the preceding 
cases. 

One difference is that the Mythryl {\tt case} expression, 
unlike the C {\tt switch} statement, may be evaluated to 
yield a value:

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    text = "three";

    i = case text
            "one"   => 1;
            "two"   => 2;
            "three" => 3;
            _       => raise exception DIE "Unsupported case";
        esac;

    printf "Result: %d\n" i;

    linux$ ./my-script
    Result: 3
\end{verbatim}

A more important difference is that the Mythryl {\tt case} 
statement performs pattern matching.  The expression 
given is conceptually matched against the patterns of its 
case clauses one by one, top to bottom, until one matches, 
at which point the corresponding expression is evaluated.

(The top-to-bottom scan is purely conceptual;  in practice 
the compiler generates highly optimized code to find select 
the appropriate case to evaluate.)

In the succeeding sections we will enumerate the different 
types of patterns supported.

\cutend*


% --------------------------------------------------------------------------------
\subsection{Tuple Patterns}
\cutdef*{subsubsection}
\label{section:ref:case-expressions-and-pattern-matching:tuple-patterns}

Tuple patterns are very simple.  They are written using syntax 
essentially identical to those of tuple expressions:  A 
comma-separated list of pattern elements wrapped in parentheses: 

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    case (1,2)
        (1,1) => print "(1,1).\n";
        (1,2) => print "(1,2).\n";
        (2,1) => print "(2,1).\n";
        (2,2) => print "(2,2).\n";
        _     => print "Dunno.\n";
    esac;

    linux$ ./my-script
    (1,2).
\end{verbatim}

What makes pattern-matching really useful is that 
we may use variables in patterns to extract values 
from the input expression:

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    case (1,2)
        (i,j) => printf "(%d,%d).\n" i j;
    esac;

    linux$ ./my-script
    (1,2).
\end{verbatim}

Another useful property is that patterns may be 
arbitrarily nested:

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    case (((1,2),(3,4,5)),(6,7))
        (((a,b),(c,d,e)),(f,g)) => printf "(((%d,%d),(%d,%d,%d)),(%d,%d))\n" a b c d e f g;
    esac;

    linux$ ./my-script
    (((1,2),(3,4,5)),(6,7))
\end{verbatim}

Note how much more compact and readable the above code is than the 
equivalent code explicitly extracting the required 
values using the underlying {\tt \#1 \#2 \#3 ...} operators:

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    x = (((1,2),(3,4,5)),(6,7));

    printf "(((%d,%d),(%d,%d,%d)),(%d,%d))\n"
        (#1 (#1 (#1 x)))
        (#2 (#1 (#1 x)))
        (#1 (#2 (#1 x)))
        (#2 (#2 (#1 x)))
        (#3 (#2 (#1 x)))
            (#1 (#2 x))
            (#2 (#2 x));

    linux$ ./my-script
    (((1,2),(3,4,5)),(6,7))
\end{verbatim}

Using a case expression to matching a tuple of Boolean values is often shorter and 
clearer than writing out the equivalent set of nested {\tt if} statements:

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    bool1 = TRUE;
    bool2 = FALSE;

    case (bool1, bool2)
       (TRUE,  TRUE ) => print "Exclusive-OR is FALSE.\n";
       (TRUE,  FALSE) => print "Exclusive-OR is TRUE.\n";
       (FALSE, TRUE ) => print "Exclusive-OR is TRUE.\n";
       (FALSE, FALSE) => print "Exclusive-OR is FALSE.\n";
    esac;

    linux$ ./my-script
    Exclusive-OR is TRUE.
\end{verbatim}

Compare with the nested-if alternative:

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    bool1 = TRUE;
    bool2 = FALSE;

    if bool1
        if bool2
            print "Exclusive-OR is FALSE.\n";
        else
            print "Exclusive-OR is TRUE.\n";
        fi;
    else
        if bool2
            print "Exclusive-OR is TRUE.\n";
        else
            print "Exclusive-OR is FALSE.\n";
        fi;
    fi;

    linux$ ./my-script
    Exclusive-OR is TRUE.
\end{verbatim}

The latter code is both longer and harder to understand and maintain 
than the former code.



\cutend*


% --------------------------------------------------------------------------------
\subsection{Sub-Pattern Matching}
\cutdef*{subsubsection}
\label{section:ref:case-expressions-and-pattern-matching:subpatterns}

Pattern variables maybe used to match entire nested structures,
not just individual elementary values:

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    case ((1,2),(3,4),(5,6))
        (a,b,c) => printf "Pairwise sums: %d, %d, %d\n" ((+)a) ((+)b) ((+)c);
    esac;

    linux$ ./my-script
    Pairwise sums: 3, 7, 11
\end{verbatim}

Here we are taking advantage of the fact that the Mythryl 
binary addition operator {\tt +} actuall operates upon 
pairs of integers;  by using the {\tt (+)} notation to 
convert it from an infix operator into a normal prefix 
function, we can apply it directly to the matched pairs 
of integers.

\cutend*

% --------------------------------------------------------------------------------
\subsection{As Patterns}
\cutdef*{subsubsection}
\label{section:ref:case-expressions-and-pattern-matching:as-patterns}

Sometimes we need to use pattern matching to assign a name to 
both a complete subpattern and also its individual constituents.

The {\tt as} pattern operator satisfies this need:

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    case ((1,2),(3,4),(5,6))
        ( pair1 as (a,b),
          pair2 as (c,d),
          pair3 as (e,f)
        )
        => printf "(%d,%d) sum to %d, (%d,%d) sum to %d, (%d,%d) sum to %d\n"
               a b ((+)pair1)
               c d ((+)pair2)
               e f ((+)pair3);
    esac;

    linux$ ./my-script
    (1,2) sum to 3, (3,4) sum to 7, (5,6) sum to 11
\end{verbatim}

\cutend*




% --------------------------------------------------------------------------------
\subsection{Record Patterns}
\cutdef*{subsubsection}
\label{section:ref:case-expressions-and-pattern-matching:record-patterns}

Record patterns are written using syntax 
essentially identical to those of record expressions:  A 
comma-separated list of pattern elements wrapped in curly brackets 
where each element consists of a {\tt name => value} pair:

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    r = { name => "Kim", age => 17 };		# Record expression.

    case r
        { name => n, age => i }			# Record pattern.
            =>
            printf "%s is %d.\n" n i;
    esac;

    linux$ ./my-script
    Kim is 17.
\end{verbatim}

Frequently record fields are pattern-matched into variables 
with the same names:

\begin{verbatim}
    #!/usr/bin/mythryl

    r = { name => "Kim", age => 17 };

    case r
        { name => name, age => age }
            =>
            printf "%s is %d.\n" name age;
    esac;

    linux$ ./my-script
    Kim is 17.
\end{verbatim}

In this case a special abbreviation is supported:

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    r = { name => "Kim", age => 17 };

    case r
        { name, age }
            =>
            printf "%s is %d.\n" name age;
    esac;

    linux$ ./my-script
    Kim is 17.
\end{verbatim}

Record patterns may be nested arbitrarily with each 
other and with other types of patterns:

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    r = { name => "Kim", coordinate => (1121, 592) };

    case r
        { name, coordinate => (i,j) }
            =>
            printf "%s is at (%d,%d).\n" name i j;
    esac;

    linux$ ./my-script
    Kim is at (1121,592).
\end{verbatim}

\cutend*

% --------------------------------------------------------------------------------
\subsection{List Patterns}
\cutdef*{subsubsection}
\label{section:ref:case-expressions-and-pattern-matching:list-patterns}

List patterns are written using syntax 
essentially identical to those of list expressions.  Lists 
may be matched in their entirety using notation 
like {\tt [ a, b, c ]}:

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    r = [ 1, 2, 3 ];				# List expression.

    case r
        [ a, b, c ] => printf "Three-element list summing to %d.\n" (a+b+c);
        [ a, b ]    => printf "Two-element list summing to %d.\n" (a+b);
        [ a ]       => printf "One-element list summing to %d.\n" a;
        []          => printf "Zero-element list summing to 0.\n";
        _           => printf "Unsupported list length.\n";
    esac;

    linux$ ./my-script
    Three-element list summing to 6.
\end{verbatim}

More typically, lists are pattern-matched into head-tail pairs 
{\tt head ! tail} and processed recursively:

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    r = [ 1, 2, 3 ];

    fun sum_list ([],       sum) => sum;
        sum_list (i ! rest, sum) => sum_list (rest, sum + i);
    end;

    printf "%d-element list summing to %d.\n" (list::length r) (sum_list (r, 0));

    linux$ ./my-script
    3-element list summing to 6.
\end{verbatim}

List patterns and other patterns may be nested arbitrarily:

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    r = [ (1,2), (3,4), (5,6) ];

    fun sum_list ([],          sum) => sum;
        sum_list (pair ! rest, sum) => sum_list (rest, sum + (+)pair);
    end;

    printf "%d-pair list summing to %d.\n" (list::length r) (sum_list (r, 0));

    linux$ ./my-script
    3-pair list summing to 21.
\end{verbatim}

\cutend*

% --------------------------------------------------------------------------------
\subsection{Pattern-Match Statement}
\cutdef*{subsubsection}
\label{section:ref:case-expressions-and-pattern-matching:pattern-match-statement}

Mythryl uses pattern matching in many contexts other than 
case statements.  The simplest is the pattern-match statement, 
which takes the form:

\begin{quotation}
~~~~my {\it pattern} = {\it expression};
\end{quotation}

This allows efficient unpacking of a nested datastructure into named components:

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    r = ( (1,2), (3,4), (5,6) );

    my ((a,b), (c,d), (e,f)) = r;

    printf "((%d,%d), (%d,%d), (%d,%d))\n" a b c d e f;

    linux$ ./my-script
    ((1,2), (3,4), (5,6))
\end{verbatim}

In the common case in which the pattern consists of a single variable,
the {\tt my} keyword may be dropped:

\begin{verbatim}
    linux$ cat my-script
    #!/usr/bin/mythryl

    i = 12 * 13;

    printf "Product = %d.\n" i;

    linux$ ./my-script
    Product = 156.
\end{verbatim}

This looks superficially much like a C assignment statement; it differs 
in that the Mythryl pattern-match statement never has any side-effects 
upon the heap;  all it does is create new local names for existing values.

\cutend*


